/* REXX */
/* SET UP DB2 CONSTANTS */
ADDRESS TSO "SUBCOM DB2V13.SDSNLOAD(dsnrexx)"
IF RC<>0 THEN DO
   S_RC=RXSUBCOM('ADD','DSNREXX','DSNREXX')
END
ADDRESS DSNREXX "CONNECT DBD1"
IF SQLCODE<>0 THEN DO
  SAY "DB2 CONNECTION FAILED, SQLCODE="SQLCODE
  EXIT 16
END
 
/* CONNECTED TO DBD1 */
/* SET UP ISPF TO FIND OUR PANELS, AND GET STARTED */
address tso
"ISPEXEC CONTROL ERRORS RETURN"
app_rc=0
ADDRESS ISPEXEC
"LIBDEF ISPPLIB DATASET ID('PRICE3D.SOURCE.PANELS')"

/* SHOW OUR SPLASH */
"ISPEXEC DISPLAY PANEL (P3DSTART)"
IF RC=8 THEN RETURN
 
/* MENU LOOP */
/* CHANGED ZCMD TO ZRET BECAUSE ZCMD IS USED BY ISPF */
ZEMSG='Connected to DB2 database.'
DO FOREVER
   CUSTEDIT='' /* BECAUSE REXX IS A BITCH */
   "ISPEXEC DISPLAY PANEL (PRICE3D)"
   IF RC=8 THEN LEAVE
   SELECT
     WHEN ZRET='1' THEN CALL ADD_FIL
     WHEN ZRET='2' THEN CALL EDIT_FIL
     WHEN ZRET='3' THEN CALL ADD_PROJ
     WHEN ZRET='4' THEN CALL EDIT_PROJ
     WHEN ZRET="5" THEN CALL NEW_JOB
     WHEN ZRET='8' THEN CALL NEW_CUST
     when ZRET='9' THEN CALL CUST_REPORT
     WHEN ZRET='C' THEN CALL CUST_TABLE
     WHEN ZRET='X' THEN LEAVE
     WHEN ZRET='x' THEN LEAVE
   OTHERWISE
     ZEMSG="Invalid menu selection.  Try again. (RC="RC") "MYKEY
   END
   ZRET=''
 END
 
ADDRESS TSO
ZRET=''

"ISPEXEC LIBDEF ISPPLIB"
"ISPEXEC LIBDEF ISPTLIB"
"ISPEXEC VRESET SHARED"
EXIT APP_RC
 
/* PROCEDURE CODE */
/* SHOW PANEL TO ADD NEW FILAMENT, THEN CALL DB2*/
ADD_FIL:
CALL RESETFILS
ADDRESS ISPEXEC
"ISPEXEC DISPLAY PANEL (PR3DFILM)"
/* MORE REFINED WAYS OF HANDLING RC NEEDED */
IF RC=0 THEN
   DO
      CALL INSERT_REC
   END
IF RC=8 THEN
   DO
     ZEMSG="ADD CANCELLED."
   END
RETURN
 
/* SHOW PANEL(S) TO REQUEST AND EDIT FILAMENT */
/* CALL DB2 UPDATE CODE AFTER COMPLETION */
EDIT_FIL:
CALL RESETFILS
/* FILMFIND REQUEST ONLY THE ITEMID */
ADDRESS ISPEXEC
"ISPEXEC DISPLAY PANEL (FILMFIND)"
IF RC=8 THEN
   DO
     ZEMSG="CANCELLED."
     RETURN
   END
ITEMEDIT=ITEMID
RESETFILS
/* CALLS DB2 SELECT CODE */
CALL READREC
IF HAVE_REC=1 THEN DO
   /* SHOW FULL FILM PANEL */
   ZFMSG=''
   "ISPEXEC DISPLAY PANEL (PR3DFILM)"
   IF RC=8 THEN DO
      ZEMSG="UPDATE CANCELLED."
      RETURN
   END
   IF STRIP(ITEMID)<>STRIP(ITEMEDIT) THEN DO
      ZFMSG= "Changing the Item ID is not allowed."
      "ISPEXEC DISPLAY PANEL (PR3DFILM)"
      IF RC=8 THEN DO
         ZEMSG="UPDATE CANCELLED."
         RETURN
      end
   end
   ELSE CALL EDIT_FILAMENT
END
RETURN
 
ADD_PROJ:
ADDRESS ISPEXEC
"ISPEXEC DISPLAY PANEL (PR3DPROJ)"
IF RC=8 THEN DO
   ZEMSG="PROJECT ADDITION CANCELLED."
   END
   CALL INSERT_PROJ
CALL RESETPROJ
RETURN
 
EDIT_PROJ:
ADDRESS ISPEXEC
"ISPEXEC DISPLAY PANEL (PROJFIND)"
IF RC=8 THEN DO
   ZEMSG="PROJECT UPDATE CANCELLED."
   CALL RESETPROJ
   RETURN
END
CALL READPROJ
IF HAVE_REC>0 THEN DO
   "ISPEXEC DISPLAY PANEL(PR3DPROJ)"
   IF RC=8 THEN DO
      ZEMSG="UPDATE CANCELLED."
      CALL RESETPROJ
   return
   end
   /* RUN THE UPDATE SQL HERE */
end
CALL RESETPROJ
RETURN
 
/* FUNCTIONS */
RESETFILS:
/* RESET ALL FILAMENT VARIABLES TO EMPTY */
   ITEMID=''
   FTYPE=''
   FCOLOR=''
   FWEIGHT=0
   FUSED=0
   FREMAINS=0
   FPRICE=0.00
   FVENDOR=''
   FVENDID=''
   FLINK=''
   FPURCHDT=''
RETURN
 
RESETPROJ:
/* RESETS ALL PROJECT VARIABLES TO EMPTY */
   PNAME=''
   PFILE=''
   PRUNTIME=0
   PCOSTMIN=0
   PMARKUP=0
   PFIL=''
   PGRAMS=''
return
 
 
/* DB2 FUNCTIONS */
 
SQLEXEC:
  ARG ACCEPT,STMT
  ADDRESS DSNREXX STMT
RETURN
 
READREC:
HAVE_REC=0
/* GENERATE THE SQL TO READ A RECORD SPECIFIED BY ITEMEDIT */
/* AND RETURN THOSE VALUES INTO THE FILAMENT VARIABLES */
SQLSTMT="SELECT ITEMID,ITTYPE,ITCOLOR,ITWEIGH,ITUSED," ,
        "ITREMAIN,ITPURCH,ITVENDOR,ITVENDORID,ITVREORDER,ITLASTDT",
        "FROM SCOTT.FILMNT WHERE ITEMID='"ITEMEDIT"'"
 
 
CALL SQLEXEC 0,"EXECSQL DECLARE C1 CURSOR FOR S1"
CALL SQLEXEC 0,"EXECSQL PREPARE S1 INTO :OUTSQLDA FROM :SQLSTMT"
 
CALL SQLEXEC 0,"EXECSQL OPEN C1"
 
 
CALL SQLEXEC 0,"EXECSQL FETCH C1 USING DESCRIPTOR :OUTSQLDA"
 
IF SQLCODE = 0 THEN DO
   /* Row found. Move data to REXX variables. */
   ITEMID=OUTSQLDA.1.SQLDATA
   FTYPE=OUTSQLDA.2.SQLDATA
   FCOLOR=OUTSQLDA.3.SQLDATA
   FWEIGHT=OUTSQLDA.4.SQLDATA
   FUSED=OUTSQLDA.5.SQLDATA
   FREMAINS=OUTSQLDA.6.SQLDATA
   FPRICE=TRUNC(OUTSQLDA.7.SQLDATA,2)
   FVENDOR=OUTSQLDA.8.SQLDATA
   FVENDID=OUTSQLDA.9.SQLDATA
   FLINK=OUTSQLDA.10.SQLDATA
   FPURCHDT=OUTSQLDA.11.SQLDATA
   HAVE_REC=1
END
ELSE IF SQLCODE = 100 THEN DO
   /* Row not found */
   ZEMSG=ITEMEDIT" was not found."
   HAVE_REC=0
END
ELSE DO
   /* Handle other errors */
   ZEMSG='(READREC) SQL ERROR 'SQLCODE ' SQLERRMC:' SQLERRMC
   HAVE_REC=2
END
 
/* CLOSE the cursor regardless of success/failure */
CALL SQLEXEC 0,"EXECSQL CLOSE C1"
RETURN
 
READPROJ:
HAVE_REC=0
/* GENERATE THE SQL TO READ A RECORD SPECIFIED BY ITEMEDIT */
/* AND RETURN THOSE VALUES INTO THE PROJECT VARIABLES */
SQLSTMT="SELECT PNAME,PFILE,PRUNTIME,PCOSTMIN,PMARKUP,PFIL,PGRAMS " ,
        "FROM SCOTT.PROJECTS WHERE PNAME='"PROJEDIT"'"
 
 
CALL SQLEXEC 0,"EXECSQL DECLARE C1 CURSOR FOR S1"
CALL SQLEXEC 0,"EXECSQL PREPARE S1 INTO :OUTSQLDA FROM :SQLSTMT"
 
CALL SQLEXEC 0,"EXECSQL OPEN C1"
 
 
CALL SQLEXEC 0,"EXECSQL FETCH C1 USING DESCRIPTOR :OUTSQLDA"
 
IF SQLCODE = 0 THEN DO
   /* Row found. Move data to REXX variables. */
   PNAME=OUTSQLDA.1.SQLDATA
   PFILE=OUTSQLDA.2.SQLDATA
   PRUNTIME=OUTSQLDA.3.SQLDATA
   PCOSTMIN=OUTSQLDA.4.SQLDATA
   PMARKUP=OUTSQLDA.5.SQLDATA
   PFIL=OUTSQLDA.6.SQLDATA
   PGRAMS=OUTSQLDA.7.SQLDATA
   HAVE_REC=1
END
ELSE IF SQLCODE = 100 THEN DO
   /* Row not found */
   ZEMSG=PROJEDIT" was not found."
   HAVE_REC=0
END
ELSE DO
   /* Handle other errors */
   ZEMSG='(READPROJ) SQL ERROR 'SQLCODE ' SQLERRMC:' SQLERRMC
   HAVE_REC=2
END
 
/* CLOSE the cursor regardless of success/failure */
CALL SQLEXEC 0,"EXECSQL CLOSE C1"
RETURN
 
INSERT_REC:
/* GENERATE AND EXECUTE SQL TO INSERT A NEW FILAMENT */
/* USING THE CURRENTLY ACTIVE FILAMENT VARIABLES */
    ADDRESS DSNREXX
    CM = "INSERT INTO SCOTT.FILMNT (ITEMID,ITTYPE,ITCOLOR," ,
         "ITWEIGH,ITUSED,ITREMAIN,ITPURCH,ITVENDOR," ,
         "ITVENDORID,ITVREORDER,ITLASTDT) VALUES(" ,
         "'"ITEMID"','"FTYPE"','"FCOLOR"'," ,
        ""FWEIGHT","FUSED","FREMAINS","TRUNC(FPRICE,2) ,
         ",'"FVENDOR"','"FVENDID"','"FLINK"'," ,
         "DATE '"FPURCHDT"')"
    "EXECSQL "CM
    IF SQLCODE = 0 THEN DO
        "EXECSQL COMMIT"
        ZEMSG=ITEMID" was added to the database."
    END
    ELSE DO
        "EXECSQL ROLLBACK"
        ZEMSG="(INSERT) SQL ERROR "SQLCODE " SQLERRMC:" SQLERRMC
    END
RETURN
 
INSERT_PROJ:
/* ADD NEW PROJECT RECORD TO DATABASE */
address DSNREXX
CM="INSERT INTO SCOTT.PROJECTS (PNAME,PFILE,PRUNTIME," ,
   "PCOSTMIN,PMARKUP,PFIL,PGRAMS) VALUES(" ,
   "'"PNAME"','"PFILE"',"PRUNTIME","PCOSTMIN","PMARKUP ,
   ",'"PFIL"',"PGRAMS")"
SAY CM
"EXECSQL "CM
IF SQLCODE=0 THEN DO
   "EXECSQL COMMIT"
   ZEMSG=PNAME" was added to the database."
end
else do
   "EXECSQL ROLLBACK"
   ZEMSG="(PROJINS) SQL ERROR "SQLCODE " SQLERRMC:" SQLERRMC
end
return

READ_CUST:
CUFOUND=0
/* GENERATE THE SQL TO READ A RECORD SPECIFIED BY ITEMEDIT */
/* AND RETURN THOSE VALUES INTO THE CUSTOMER VARIABLES */
SQLSTMT="SELECT CUSTID,CNAME,ADDR1,ADDR2,CITY,STATE,ZIP,PHONE,EMAIL" ,
        " FROM SCOTT.P3DCUST WHERE CUSTID='"CUSTEDIT"'"
CALL SQLEXEC 0,"EXECSQL DECLARE C1 CURSOR FOR S1"
CALL SQLEXEC 0,"EXECSQL PREPARE S1 INTO :OUTSQLDA FROM :SQLSTMT"
CALL SQLEXEC 0,"EXECSQL OPEN C1"
CALL SQLEXEC 0,"EXECSQL FETCH C1 USING DESCRIPTOR :OUTSQLDA"
IF SQLCODE = 0 THEN DO
   /* Row found. Move data to REXX variables. */
   CUSTID=OUTSQLDA.1.SQLDATA
   CNAME=OUTSQLDA.2.SQLDATA
   ADDR1=OUTSQLDA.3.SQLDATA
   ADDR2=OUTSQLDA.4.SQLDATA
   CITY=OUTSQLDA.5.SQLDATA
   ST=OUTSQLDA.6.SQLDATA
   ZIP=OUTSQLDA.7.SQLDATA
   PHONE=OUTSQLDA.8.SQLDATA
   EMAIL=OUTSQLDA.9.SQLDATA
   CUFOUND=1
END
ELSE IF SQLCODE = 100 THEN DO
   /* Row not found */
   ZEMSG=CUSTEDIT" was not found."
   CUFOUND=0
END
ELSE DO
   /* Handle other errors */
   ZEMSG='(READCUST) SQL ERROR 'SQLCODE ' SQLERRMC:' SQLERRMC
   HAVE_REC=2
END
 
/* CLOSE the cursor regardless of success/failure */
CALL SQLEXEC 0,"EXECSQL CLOSE C1"


RETURN

INSERT_CUST:
ADDRESS DSNREXX
CM="INSERT INTO SCOTT.P3DCUST (CUSTID,CNAME,ADDR1,ADDR2,CITY,STATE," ,
   "ZIP,PHONE,EMAIL) VALUES ('"CUSTID"','"CNAME"','"ADDR1"','"ADDR2"','" ,
   ""CITY"','"ST"','"ZIP"','"PHONE"','"EMAIL"')"
"EXECSQL "CM
SAY SQLCODE
IF SQLCODE=0 THEN DO 
   "EXECSQL COMMIT"
   ZEMSG=CUSTID" was added to the database."
end 
else do 
   "EXECSQL ROLLBACK"
   ZEMSG="(CUSTINS) SQL ERROR "SQLCODE " SQLERRMC:" SQLERRMC
end
return


 
EDIT_FILAMENT:
/* AGGRESSIVE CLEANING OF THE KEY FIELD FROM ISPF PANEL */
CLEAN_ITEMID = STRIP(ITEMID, 'B')
CLEAN_ITEMID = translate(CLEAN_ITEMID)
/* UPDATE THE RECORD WITH ALL CURRENT FILAMENT DATA */
/* NOTE THIS WILL NOT ALLOW THE ITEMID TO BE CHANGED */
CM="UPDATE SCOTT.FILMNT ",
   "SET ITTYPE='"FTYPE"',",
   "ITCOLOR='"FCOLOR"',",
   "ITWEIGH="FWEIGHT",",
   "ITUSED="FUSED",",
   "ITREMAIN="FREMAINS",",
   "ITPURCH="TRUNC(FPRICE,2)",",
   "ITVENDOR='"FVENDOR"',",
   "ITVENDORID='"FVENDID"',",
   "ITVREORDER='"FLINK"',",
   "ITLASTDT=DATE '"FPURCHDT"'",
   "WHERE RTRIM(ITEMID)='"CLEAN_ITEMID"'"
ADDRESS DSNREXX
"EXECSQL "CM
IF SQLCODE = 0 THEN DO
    /* Check if any rows were affected */
    IF SQLERRD.3 > 0 THEN DO
        UPDROWS=SQLERRD.3
        "EXECSQL COMMIT"
        ZEMSG="("UPDROWS") updated."
    END
    ELSE DO
        /* Handles those weird times when the update doesn't actually update */
        "EXECSQL ROLLBACK" /* Still a good practice */
        ZEMSG="UPDATE FAILED: ITEMID '"ITEMID"' NOT FOUND. ("SQLERRD.3")"
    END
END
ELSE DO
    /* Handle SQL error */
    "EXECSQL ROLLBACK"
    ZEMSG="SQL ERROR "SQLCODE " SQLERRMC:" SQLERRMC
END
RETURN

RESET_CUST:
CUSTID=''
CNAME=''
ADDR1=''
ADDR2=''
CITY=''
ST=''
ZIP=''
PHONE=''
EMAIL=''
CUSTEDIT=''
CUFOUND=0
return

EDIT_CUST:
/* AGGRESSIVE CLEANING OF THE KEY FIELD FROM ISPF PANEL */
CLEAN_CUSTID = STRIP(CUSTID, 'B')
CLEAN_CUSTID = translate(CLEAN_CUSTID)
/* UPDATE THE RECORD WITH ALL CURRENT CUSTOMER DATA */
/* NOTE THIS WILL NOT ALLOW THE CUSTID TO BE CHANGED */
CM="UPDATE SCOTT.P3DCUST ",
   "SET",
   "CNAME='"CNAME"',",
   "ADDR1='"ADDR1"',",
   "ADDR2='"ADDR2"',",
   "CITY='"CITY"',",
   "STATE='"ST"',",
   "ZIP='"ZIP"',",
   "PHONE='"PHONE"',",
   "EMAIL='"EMAIL"'",
   "WHERE RTRIM(CUSTID)='"CLEAN_CUSTID"'"
ADDRESS DSNREXX

"EXECSQL "CM


IF SQLCODE = 0 THEN DO
    /* Check if any rows were affected */
    IF SQLERRD.3 > 0 THEN DO
        UPDROWS=SQLERRD.3
        "EXECSQL COMMIT"
        ZEMSG="("UPDROWS") updated."
    END
    ELSE DO
        /* Handles those weird times when the update doesn't actually update */
        "EXECSQL ROLLBACK" /* Still a good practice */
        ZEMSG="UPDATE FAILED: CUSTID '"CUSTID"' NOT FOUND. ("SQLERRD.3")"
    END
END
ELSE DO
    /* Handle SQL error */
    "EXECSQL ROLLBACK"
    ZEMSG="SQL ERROR "SQLCODE " SQLERRMC:" SQLERRMC
END
RETURN

NEW_JOB:
Address ISPEXEC "DISPLAY PANEL(PD3JOB)"
SAVE_RC = RC
IF SAVE_RC = 8 THEN DO
   ZEMSG = "NEW JOB CANCELLED."
   RETURN
   end
else do
  SAY SAVE_RC
END
RETURN
 
NEW_CUST:

IF STRIP(CUSTEDIT)='' Then DO
Address ISPEXEC "DISPLAY PANEL(P3DCUENT)"
SAVE_RC=RC
IF SAVE_RC=8 THEN DO
   ZEMSG="NEW CUSTOMER CANCELLED."
   CALL RESET_CUST
   return
end
END
/* DO THE LOOKUP AND RETURN CUFOUND AS 1 IF IT'S EXISTING */
/* OTHERWISE RETURN CUFOUND=0 */
CUSTEDIT=CUSTID
CALL READ_CUST
Address ISPEXEC "DISPLAY PANEL(P3DCUST)"
SAVE_RC=RC 
if save_rc=8 then do
   zemsg="CUSTOMER UPDATE CANCELLED."
   CALL RESET_CUST
   return
end
IF CUFOUND=0 Then do
   call INSERT_CUST
end
ELSE DO 
   CALL EDIT_CUST
END
CALL RESET_CUST
return

CUST_REPORT:
out_dd = "REPORT"

Address TSO "ALLOCATE FI("out_dd") SYSOUT(A)"
IF RC<>0 Then SAY "ALLOCATE RC="RC
 /* Initialize Pagination */
  PAGE_NUM   = 0
  LINE_COUNT = 99 
  MAX_LINES  = 55 
  OFFSET     = COPIES(" ", 12) /* Align below CNAME column */
  TOT_RECS   = 0
  SQLSTMT="SELECT * FROM SCOTT.P3DCUST"
  CALL SQLEXEC 0,"EXECSQL DECLARE C1 CURSOR FOR S1"
  CALL SQLEXEC 0,"EXECSQL PREPARE S1 INTO :OUTSQLDA FROM :SQLSTMT"
  CALL SQLEXEC 0,"EXECSQL OPEN C1"

  do forever
    CALL SQLEXEC 0,"EXECSQL FETCH C1 USING DESCRIPTOR :OUTSQLDA"
    if SQLCODE = 100 then leave
    if SQLCODE < 0 then leave
    
    CUSTID = OUTSQLDA.1.SQLDATA
    CNAME  = OUTSQLDA.2.SQLDATA
    ADDR1  = OUTSQLDA.3.SQLDATA
    ADDR2  = OUTSQLDA.4.SQLDATA
    CITY   = OUTSQLDA.5.SQLDATA
    ST     = OUTSQLDA.6.SQLDATA
    ZIP    = OUTSQLDA.7.SQLDATA
    PHONE  = OUTSQLDA.8.SQLDATA
    EMAIL  = OUTSQLDA.9.SQLDATA


    if LINE_COUNT + 4 > MAX_LINES then do
       PAGE_NUM = PAGE_NUM + 1
       
       out_line.1 = CENTER("CUSTOMER CONTACT & ADDRESS REPORT", 120) || "PAGE:" || RIGHT(PAGE_NUM, 3)
       address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."
       out_line.1 = COPIES("-", 132)
       address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."
       

       out_line.1 = LEFT("CUST-ID", 12) || LEFT("NAME", 30) || LEFT("PHONE", 20) || "EMAIL"
       address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."
       out_line.1 = COPIES("-", 132)
       address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."
       
       LINE_COUNT = 4
    end


    out_line.1 = LEFT(CUSTID, 12) || LEFT(CNAME, 30) || LEFT(PHONE, 20) || EMAIL
    address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."


    out_line.1 = OFFSET || ADDR1
    address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."


    if STRIP(ADDR2) \= "" then out_line.1 = OFFSET || ADDR2
    else out_line.1 = " " 
    address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."


    out_line.1 = OFFSET || STRIP(CITY) || ", " || ST || "  " || ZIP
    address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."


    out_line.1 = " "
    address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."
    TOT_RECS=TOT_RECS+1
    LINE_COUNT = LINE_COUNT + 5
  end
  out_line.1 = COPIES("=", 132)
  address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."
  out_line.1 = "TOTAL RECORDS PROCESSED: " || TOT_RECS
  address TSO "EXECIO 1 DISKW" out_dd "(STEM out_line."

  CALL SQLEXEC 0,"EXECSQL CLOSE C1"
  address TSO "EXECIO 0 DISKW" out_dd "(FINIS)"
  address TSO "FREE FI("out_dd")"
  zemsg="CUSTOMER REPORT SENT TO CLASS A PRINTER."
RETURN 0


CUST_TABLE: PROCEDURE
  ADDRESS ISPEXEC

  do forever

    CALL LOAD_DATA
    "TBTOP CUSTBL"
    

    SEL = ""
    "VPUT(SEL) SHARED"


    do forever
      "TBDISPL CUSTBL PANEL(P3DCUTBL)"
      DISP_RC = RC
      

      if DISP_RC >= 8 then return 0


      if ZTDSELS > 0 then do

         "VGET(SEL CUSTID CNAME)"
         
         SELECT
           WHEN SEL = 'S' THEN CALL SHOW_REC
           WHEN SEL = 'E' THEN CALL EDIT_REC
           WHEN SEL = 'D' THEN CALL DELETE_REC
           OTHERWISE NOP
         END
         

         leave 
      end
      
    end 
  end
RETURN 0


LOAD_DATA:
  "TBSTATS CUSTBL"
  if RC = 0 then "TBEND CUSTBL"

  "TBCREATE CUSTBL NAMES(CUSTID CNAME CITY STATE) NOWRITE"

  SQLSTMT = "SELECT CUSTID, CNAME, CITY, STATE FROM SCOTT.P3DCUST"
  CALL SQLEXEC 0,"EXECSQL DECLARE C1 CURSOR FOR S1"
  CALL SQLEXEC 0,"EXECSQL PREPARE S1 INTO :OUTSQLDA FROM :SQLSTMT"
  CALL SQLEXEC 0,"EXECSQL OPEN C1"

  do forever
    CALL SQLEXEC 0,"EXECSQL FETCH C1 USING DESCRIPTOR :OUTSQLDA"
    if SQLCODE = 100 then leave
    
    CUSTID = OUTSQLDA.1.SQLDATA  
    CNAME  = OUTSQLDA.2.SQLDATA  
    CITY   = OUTSQLDA.3.SQLDATA  
    ST     = OUTSQLDA.4.SQLDATA 
    
    SEL = "" 
    "TBADD CUSTBL"
  end
  CALL SQLEXEC 0,"EXECSQL CLOSE C1"
RETURN


SHOW_REC:
   SAY "SHOWING:" STRIP(CNAME)
RETURN

EDIT_REC:
   CUSTEDIT= STRIP(CUSTID)
   CALL NEW_CUST
RETURN

DELETE_REC:
   SAY "DELETING:" STRIP(CUSTID)
RETURN
